plugins {
  id 'org.springframework.boot' version '3.4.4'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'java'
}

allprojects {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'

  group = 'io.devfactory'
  version = '0.0.1'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  repositories {
    mavenCentral()
  }

  ext {
    set('springCloudVersion', '2023.0.3')
  }

  configurations {
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  dependencies {
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  tasks.named('test') {
    useJUnitPlatform()
  }

  tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
  }

  bootRun {
    if (project.hasProperty('args')) {
      //noinspection GroovyAssignabilityCheck
      args project.args.split(',') as String[]
    }
    systemProperties = System.properties as Map<String, ?>
  }
}

subprojects {
  dependencies {
    if (project.name.startsWith('service') && project.name != 'service-common') {
      implementation project(':service-common')
    }
    implementation 'org.apache.commons:commons-lang3:3.15.0'
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }
}
